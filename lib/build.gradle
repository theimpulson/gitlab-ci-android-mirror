plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.protobuf'
    id 'maven-publish'
    id 'org.jlleitschuh.gradle.ktlint'
}

android {
    namespace 'com.aurora.gplayapi'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33
        aarMetadata {
            minCompileSdk = 21
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    packagingOptions {
        resources {
            excludes += '**/*.proto'
        }
    }
}

dependencies {

    implementation 'com.google.protobuf:protobuf-javalite:3.22.3'
    implementation 'com.github.kittinunf.fuel:fuel:2.3.1'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.22.3'
    }
    generateProtoTasks {
        all().configureEach { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

// Run "./gradlew publishReleasePublicationToLocalRepository" to generate release AAR locally
publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.aurora'
            artifactId = 'gplayapi'
            version = '3.1.3'

            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        maven {
            name = 'local'
            url = "${project.buildDir}/repo"
        }
    }
}
