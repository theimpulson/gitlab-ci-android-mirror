plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.protobuf'
    id 'maven-publish'
}

android {
    namespace 'com.aurora.gplayapi'
    compileSdk 33

    defaultConfig {
        minSdk 19
        targetSdk 33
        aarMetadata {
            minCompileSdk = 19
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    packagingOptions {
        resources {
            excludes += '**/*.proto'
        }
    }
}

dependencies {

    implementation 'com.google.protobuf:protobuf-javalite:3.22.3'
    implementation 'com.github.kittinunf.fuel:fuel:2.3.1'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.22.3'
    }
    generateProtoTasks {
        all().configureEach { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

// Run "./gradlew publishReleasePublicationToLocalRepository" to generate release AAR locally
publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.aurora'
            artifactId = 'gplayapi'
            version = '3.1.1'

            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        maven {
            name = 'local'
            url = "${project.buildDir}/repo"
        }
    }
}
